# $Id$
# Maintainer: Laurent Carlier <lordheavym@gmail.com>

pkgname=libxfont2
pkgver=2.0.2
pkgrel=1
pkgdesc="X11 font rasterisation library"
arch=(i686 x86_64)
url="https://xorg.freedesktop.org/"
license=('custom')
depends=('freetype2' 'libfontenc' 'xproto' 'fontsproto')
makedepends=('xorg-util-macros' 'xtrans')
source=(${url}/archive/individual/lib/libXfont2-${pkgver}.tar.bz2{,.sig})
sha256sums=('94088d3b87f7d42c7116d9adaad155859e93330c6e47f5989f2de600b9a6c111'
            'SKIP')
validpgpkeys=('C383B778255613DFDB409D91DB221A6900000011') # Keith Packard <keithp@keithp.com>
validpgpkeys+=('995ED5C8A6138EB0961F18474C09DD83CAAA50B2') # "Adam Jackson <ajax@nwnk.net>"

build() {
  cd "${srcdir}/libXfont2-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --disable-static
  make
}

package() {
  cd "${srcdir}/libXfont2-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
