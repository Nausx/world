# $Id$
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=at-spi2-core
pkgver=2.26.1
pkgrel=1
pkgdesc="Protocol definitions and daemon for D-Bus at-spi"
url="https://git.gnome.org/browse/at-spi2-core"
arch=(i686 x86_64)
license=(GPL2)
depends=(dbus glib2 libxtst)
makedepends=(intltool gobject-introspection git gtk-doc meson)
_commit=832febae6759636ce7abf98a93738d60a9726a3b  # tags/AT_SPI2_CORE_2_26_1^0
source=("git+https://git.gnome.org/browse/at-spi2-core#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^AT_SPI2_CORE_//;s/_/./g;s/-/+/g'
}

prepare() {
  mkdir build
  cd $pkgname
}
  
build() {
  cd build
  arch-meson ../$pkgname -Denable_docs=true
  ninja
}

package() {
  cd build
  DESTDIR="$pkgdir" ninja install
}

# vim:set ts=2 sw=2 et:
