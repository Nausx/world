# Maintainer: artoo <artoo@cromnix.org>
# Contributor: Philip MÃ¼ller <philm[at]manjaro[dog]org>

_commit=4b176eba0680eb1446e0f04d5013965d0560bfa1

pkgname=calamares
pkgver=3.1.5
pkgrel=3
pkgdesc='Distribution-independent installer framework'
arch=('i686' 'x86_64')
license=(GPL)
url="https://github.com/artix-linux/calamares"
license=('LGPL')
depends=('kconfig' 'kcoreaddons' 'ki18n' 'solid' 'yaml-cpp' 'kpmcore>=3.1.0'
         'boost-libs' 'hwinfo' 'qt5-svg' 'polkit-qt5' 'gtk-update-icon-cache' 'pythonqt')
makedepends=('extra-cmake-modules' 'qt5-tools' 'qt5-translations' 'boost')
backup=('usr/share/calamares/modules/bootloader.conf'
        'usr/share/calamares/modules/displaymanager.conf'
        'usr/share/calamares/modules/initcpio.conf'
        'usr/share/calamares/modules/unpackfs.conf')

source=("$pkgname-$pkgver.tar.gz::https://github.com/artix-linux/calamares/archive/${_commit}.tar.gz")
sha256sums=('05747999d8dca2ee2a50a09f06c2f2ff880415b38e2a646aafc277370182916b')

# pkgver() {
#     cd $pkgname
#     git describe --tags | sed 's/-/+/g'
# }

prepare() {
	mv ${srcdir}/calamares-${_commit} ${srcdir}/calamares-${pkgver}
	cd ${srcdir}/calamares-${pkgver}
	# patches here
}

build() {
	cd ${srcdir}/calamares-${pkgver}

	mkdir -p build
	cd build
        cmake .. \
              -DCMAKE_BUILD_TYPE=Release \
              -DCMAKE_INSTALL_PREFIX=/usr \
              -DCMAKE_INSTALL_LIBDIR=lib \
              -DWITH_PYTHONQT:BOOL=ON \
              -DSKIP_MODULES="webview interactiveterminal initramfs \
                              initramfscfg dracut dracutlukscfg \
                              dummyprocess dummypython dummycpp \
                              dummypythonqt"
        make
}

package() {
	cd ${srcdir}/calamares-${pkgver}/build
	make DESTDIR="$pkgdir" install

    install -dm755 ${pkgdir}/etc/calamares/modules
    cd ${srcdir}/calamares-${pkgver}/data/artix/modules
    for conf in machineid.conf finished.conf displaymanager.conf welcome.conf umount.conf locale.conf;do
        install -m755 $conf ${pkgdir}/etc/calamares/modules/$conf
    done
    install -m755 ${srcdir}/calamares-${pkgver}/data/artix/settings.conf ${pkgdir}/etc/calamares/settings.conf
}
