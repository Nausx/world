# $Id$
# Maintainer: Laurent Carlier <lordheavym@gmail.com>

pkgname=xf86-video-amdgpu
pkgver=1.3.0
pkgrel=1
pkgdesc="X.org amdgpu video driver"
arch=('i686' 'x86_64')
url="https://xorg.freedesktop.org/"
license=('custom')
depends=('libeudev' 'mesa')
makedepends=('xorg-server-devel' 'eudev' 'X-ABI-VIDEODRV_VERSION=23')
conflicts=('xorg-server<1.19.0' 'X-ABI-VIDEODRV_VERSION<23' 'X-ABI-VIDEODRV_VERSION>=24')
groups=('xorg-drivers')
source=(${url}/releases/individual/driver/${pkgname}-${pkgver}.tar.bz2{,.sig})
sha256sums=('c1630f228938be949273f72b29ae70822dde064ad79c3ccb14d55f427e3f4e70'
            'SKIP')
validpgpkeys=('B09FAF35BE914521980951145A81AF8E6ADBB200') # Michel Daenzer <michel@daenzer.net>

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix=/usr \
    --enable-glamor
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make check
}

package() {
  cd ${pkgname}-${pkgver}

  make "DESTDIR=${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
