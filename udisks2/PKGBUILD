# Maintainer: Alexey D. <lq07829icatm@rambler.ru>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=udisks2
pkgver=2.7.4
pkgrel=1
pkgdesc="Disk Management Service, version 2"
arch=('i686' 'x86_64')
url="http://www.freedesktop.org/wiki/Software/udisks"
license=('GPL2')
depends=('polkit' 'libatasmart' 'libgudev' 'eudev' 'libblockdev')
makedepends=('docbook-xsl' 'gobject-introspection' 'gnome-common' 'intltool' 'parted' 'libiscsi')
optdepends=('gptfdisk: GUID partition table support'
            'ntfs-3g: NTFS filesystem management support'
            'dosfstools: VFAT filesystem management support'
            'libiscsi: iSCSI support')
source=(https://github.com/storaged-project/udisks/archive/udisks-$pkgver.tar.gz)
sha256sums=('2ca70f67cdfb4bd5b630972f72fd29eb35eb1b644e161132488446776a1d70fc')

prepare(){
#     cd "udisks-udisks-$pkgver"
    sed -e 's/AC_MSG_ERROR(\[libstoragemgmt/AC_MSG_WARN([libstoragemgmt/' \
        -e 's/AC_MSG_ERROR(\[libconfig/AC_MSG_WARN([libconfig/' \
        -i udisks-udisks-$pkgver/configure.ac
}

build() {
    cd "udisks-udisks-$pkgver"
    #export LANG=en_US.UTF-8
    ./autogen.sh \
        --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --disable-static \
        --sbindir=/usr/bin \
        --libexecdir=/usr/lib \
        --with-systemdsystemunitdir=no \
        --enable-available-modules
    make
}

check() {
    cd udisks-udisks-$pkgver
    make check
}

package() {
    cd "udisks-udisks-$pkgver"
    make DESTDIR="$pkgdir" install \
        bash_completiondir=/usr/share/bash-completion/completions
}
