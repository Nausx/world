# Maintainer: artoo <artoo@cromnix.org>

pkgbase=artools
pkgname=('artools-base' 'artools-pkg' 'artools-iso-base'  'artools-yaml' 'artools-iso')
pkgver=0.5.2
pkgrel=1
arch=('any')
pkgdesc='Development tools for Artix'
license=('GPL')
groups=('artools')
url='https://github.com/artix-linux/artools'
# makedepends=('git')
source=("${pkgbase}-${pkgver}.tar.gz::$url/archive/${pkgver}.tar.gz")
sha256sums=('503a9f34b700de3341d388a9b7deddfcf3f3c5f6b565f6f2b19d7dd211340f63')

prepare() {
    cd ${srcdir}/${pkgbase}-${pkgver}
    sed -e "s/^Version=.*/Version=${pkgver}/" -i Makefile
    # patches here
}

build() {
    cd ${srcdir}/${pkgbase}-${pkgver}
    make SYSCONFDIR=/etc PREFIX=/usr
}

package_artools-base() {
    pkgdesc='Development tools for Artix (base tools)'
    depends=('openssh' 'rsync' 'haveged' 'os-prober' 'gnupg' 'pacman')
    optdepends=('artools-pkg: Artix package tools'
                'artools-iso: Artix iso tools'
                'artools-yaml: Artix yaml tools')
    backup=('etc/artools/artools.conf')

    cd ${srcdir}/${pkgbase}-${pkgver}
    make SYSCONFDIR=/etc PREFIX=/usr DESTDIR=${pkgdir} install_base
}

package_artools-pkg() {
    pkgdesc='Development tools for Artix (packaging tools)'
    depends=('artools-base' 'namcap' 'wget')
    conflicts=('devtools')

    cd ${srcdir}/${pkgbase}-${pkgver}
    make SYSCONFDIR=/etc PREFIX=/usr DESTDIR=${pkgdir} install_pkg
}

package_artools-yaml() {
    pkgdesc='Development tools for Artix (yaml tools)'
    depends=('artools-base' 'artools-iso-base')

    cd ${srcdir}/${pkgbase}-${pkgver}
    make SYSCONFDIR=/etc PREFIX=/usr DESTDIR=${pkgdir} install_yaml
}

package_artools-iso-base() {
    pkgdesc='Development tools for Artix (ISO base profile)'

    cd ${srcdir}/${pkgbase}-${pkgver}
    make SYSCONFDIR=/etc PREFIX=/usr DESTDIR=${pkgdir} install_isobase
}

package_artools-iso() {
    pkgdesc='Development tools for Artix (ISO tools)'
    depends=('artools-yaml' 'dosfstools' 'libisoburn' 'squashfs-tools' 'mkinitcpio' 'mktorrent' 'grub')

    cd ${srcdir}/${pkgbase}-${pkgver}
    make SYSCONFDIR=/etc PREFIX=/usr DESTDIR=${pkgdir} install_iso
}

