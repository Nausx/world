# Maintainer: artoo <artoo@manjaro.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=sddm
pkgver=0.14.0
pkgrel=8
pkgdesc='QML based X11 display manager'
arch=('i686' 'x86_64')
url='http://github.com/sddm/sddm'
license=('GPL')
# provides=('display-manager' 'sddm')
provides=('display-manager')
depends=('qt5-declarative')
makedepends=('extra-cmake-modules' 'python-docutils' 'qt5-tools' 'elogind')
# optdepends=('displaymanager-openrc: displaymanager initscript')
# conflicts=('display-manager' 'sddm' 'sddm-consolekit')
# replaces=('sddm-consolekit')
install="${pkgname}.install"
backup=('usr/share/sddm/scripts/Xsetup'
        'etc/pam.d/sddm'
        'etc/sddm.conf')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/${pkgname}/${pkgname}/archive/v${pkgver}.tar.gz"
        "sddm-fix-avatars.patch::https://github.com/sddm/sddm/commit/ecb903e4.patch"
        'sddm-pam.patch'
        'sddm-enable-elogind.patch')
sha256sums=('a551551a6ba324e9c384c89bc63e871de65fea3740eadbea2d63df86045f8205'
            'a2bcdc20bb8915ed4eae810832266ff3bd22a523813ef1674eb25a28800e4368'
            '151b9b4a16b919a4f3a6564b2cd794effdc07773c47f2d6b860bbd467c3e4798'
            'cff3f483415c95784d6b9977c31b89fac734d0d74facc4ac042ed8de970d5007')

prepare() {
	mkdir -p build
	cd ${pkgname}-${pkgver}
	patch -p1 -i ${srcdir}/sddm-pam.patch
	patch -p1 -i ${srcdir}/sddm-enable-elogind.patch
}

build() {
	cd build
	cmake ../${pkgname}-${pkgver} \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=Release \
		-DENABLE_PAM=yes \
		-DNO_SYSTEMD=yes \
		-DUSE_ELOGIND=yes \
		-DCMAKE_INSTALL_LIBEXECDIR=/usr/lib/sddm \
		-DDBUS_CONFIG_FILENAME=org.freedesktop.sddm.conf \
		-DBUILD_MAN_PAGES=ON
	make
}

package() {
	cd build
	make DESTDIR="${pkgdir}" install
}
