# Maintainer: Chris Cromer <chris@cromer.cl>
# Contributor: Guillaume Benoit <guillaume@manjaro.org>

pkgbase=pamac
pkgname=('pamac' 'pamac-tray-appindicator')
_pkgver=6.0.2
pkgver=$_pkgver
pkgrel=2
pkgdesc="A Gtk3 frontend for libalpm"
arch=('i686' 'x86_64')
url="https://github.com/manjaro/pamac"
license=('GPL3')
depends=('glib2>=2.42' 'json-glib' 'libsoup' 'dbus-glib' 'polkit' 'vte3>=0.38' 'gtk3>=3.22'
         'libnotify' 'desktop-file-utils' 'pacman>=5.0' 'pacman<5.1' 'gnutls>=3.4'
         'appstream-glib' 'archlinux-appstream-data')
makedepends=('gettext' 'itstool' 'vala>=0.36' 'libappindicator-gtk3')
options=(!emptydirs)

source=("pamac-$pkgver-$pkgrel.tar.gz::$url/archive/v$_pkgver.tar.gz"
       "fix-apps-desc.patch::https://github.com/manjaro/pamac/commit/8f19ea1887bfd325cb3f94324dc8d123e005e01f.patch"
       "aur-fix.patch::https://github.com/manjaro/pamac/commit/bad4624c3bda629d1e5e6a3bc575031502b75b39.patch")

sha256sums=('db0bc0477bb687d99e60c74e15367f5df9d3d9450085800aae5dabcc9b43bcb6'
            '151068631cc710f4e4396a67c7efe0fda2ba42766dba64fbe271ec50789a08c0'
            '7b6128a1cf7f59e6bca292af8d691974a1fe4c95e45100c811c09b15a3485644')

prepare() {
  cd "$srcdir/pamac-$_pkgver"
  # patches here
  patch -Np1 -i "$srcdir"/fix-apps-desc.patch
  patch -Np1 -i "$srcdir"/aur-fix.patch

  # adjust version string
  sed -i -e "s|\"$_pkgver\"|\"$pkgver-$pkgrel\"|g" src/manager_window.vala
}

build() {
  cd "$srcdir/pamac-$_pkgver"

  # build
  make all
}

package_pamac() {
  optdepends=('polkit-gnome: needed for authentification in Cinnamon, Gnome'
              'lxsession: needed for authentification in Xfce, LXDE etc.'
              'pamac-tray-appindicator: tray icon for KDE')
  backup=('etc/pamac.conf')
  replaces=('pamac-aur')
  install=pamac.install
  cd "$srcdir/pamac-$_pkgver"
  make prefix="$pkgdir"/usr sysconfdir="$pkgdir"/etc install
}

package_pamac-tray-appindicator() {
  pkgdesc="Tray icon using appindicator which feets better in KDE"
  depends=('pamac' 'libappindicator-gtk3')
  cd "$srcdir/pamac-$_pkgver"
  make use_appindicator=true
  make prefix="$pkgdir"/usr sysconfdir="$pkgdir"/etc install_pamac-tray-appindicator
}
# vim:set ts=2 sw=2 et:
