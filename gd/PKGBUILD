# $Id$
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=gd
pkgver=2.2.4
pkgrel=2
pkgdesc="Library for the dynamic creation of images by programmers"
arch=('i686' 'x86_64')
url="http://www.libgd.org/"
license=('custom')
depends=('fontconfig' 'libxpm' 'libwebp')
optdepends=('perl: bdftogd script')
checkdepends=('ttf-liberation')
source=("https://github.com/libgd/libgd/releases/download/gd-${pkgver}/libgd-${pkgver}.tar.xz"
        'gd-2.2.4-upstream.patch')
md5sums=('a244855a323a3ea1975d708eb1e12b7a'
         '7db6743a61b30471d0b466e6a6c54a49')

prepare() {
  cd libgd-${pkgver}
  patch -p1 -i ${srcdir}/gd-2.2.4-upstream.patch
}

build() {
  cd libgd-${pkgver}
  ./configure \
    --prefix=/usr \
    --disable-rpath
  make
}

check() {
  cd libgd-${pkgver}
  # see https://github.com/libgd/libgd/issues/302
  [[ ${CARCH} == 'i686' ]] || FREETYPE_PROPERTIES='truetype:interpreter-version=35' make check
}

package() {
  cd libgd-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
